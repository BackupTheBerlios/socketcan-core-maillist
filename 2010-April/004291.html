<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<HTML>
 <HEAD>
   <TITLE> [PATCH 3/3] add autotools infrastructure
   </TITLE>
   <LINK REL="Index" HREF="http://lists.berlios.de/pipermail/socketcan-core/2010-April/index.html" >
   <LINK REL="made" HREF="mailto:socketcan-core%40lists.berlios.de?Subject=Re%3A%20%5BPATCH%203/3%5D%20add%20autotools%20infrastructure&In-Reply-To=%3C1271451106-18030-4-git-send-email-mkl%40pengutronix.de%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <style type="text/css">
       pre {
           white-space: pre-wrap;       /* css-2.1, curent FF, Opera, Safari */
           }
   </style>
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="004296.html">
   <LINK REL="Next"  HREF="004293.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[PATCH 3/3] add autotools infrastructure</H1>
    <B>Marc Kleine-Budde</B> 
    <A HREF="mailto:socketcan-core%40lists.berlios.de?Subject=Re%3A%20%5BPATCH%203/3%5D%20add%20autotools%20infrastructure&In-Reply-To=%3C1271451106-18030-4-git-send-email-mkl%40pengutronix.de%3E"
       TITLE="[PATCH 3/3] add autotools infrastructure">mkl at pengutronix.de
       </A><BR>
    <I>Fri Apr 16 22:51:46 CEST 2010</I>
    <P><UL>
        <LI>Previous message: <A HREF="004296.html">[PATCH 2/3] add .gitignore
</A></li>
        <LI>Next message: <A HREF="004293.html">[PATCH 0/3] RFC: autotoolization
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#4291">[ date ]</a>
              <a href="thread.html#4291">[ thread ]</a>
              <a href="subject.html#4291">[ subject ]</a>
              <a href="author.html#4291">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Signed-off-by: Marc Kleine-Budde &lt;<A HREF="https://lists.berlios.de/mailman/listinfo/socketcan-core">mkl at pengutronix.de</A>&gt;
---
 GNUmakefile.am                             |   79 ++++++++++++++
 autogen.sh                                 |   22 ++++
 configure.ac                               |  156 ++++++++++++++++++++++++++++
 3 files changed, 257 insertions(+), 0 deletions(-)
 create mode 100644 GNUmakefile.am
 create mode 100755 autogen.sh
 create mode 100644 config/m4/.secret-world-domination-project
 create mode 100644 configure.ac

diff --git a/GNUmakefile.am b/GNUmakefile.am
new file mode 100644
index 0000000..668dc0f
--- /dev/null
+++ b/GNUmakefile.am
@@ -0,0 +1,79 @@
+AM_CPPFLAGS = \
+	-I$(top_srcdir)/include \
+	-I$(top_builddir)/include \
+	$(linux_CFLAGS)
+
+# link every app against libcan, it's static so it wouldn't hurt
+LDADD = \
+	libcan.la
+
+noinst_HEADERS = \
+	lib.h \
+	terminal.h
+
+
+noinst_LTLIBRARIES = \
+	libcan.la
+
+libcan_la_SOURCES = \
+	lib.c
+
+
+bin_PROGRAMS = \
+	asc2log \
+	bcmserver \
+	canbusload \
+	candump \
+	canfdtest \
+	cangen \
+	canlogserver \
+	canplayer \
+	cansend \
+	cansniffer \
+	log2asc \
+	log2long \
+	slcan_attach \
+	slcand \
+	slcanpty
+
+if CONFIG_GW
+bin_PROGRAMS += \
+	cangw
+endif
+
+if CONFIG_ISOTP
+bin_PROGRAMS += \
+	isotpdump \
+	isotprecv \
+	isotpsend \
+	isotpsniffer \
+	isotptun
+endif
+
+EXTRA_DIST = \
+	autogen.sh
+
+MAINTAINERCLEANFILES = \
+	configure \
+	GNUmakefile.in \
+	aclocal.m4 \
+	config/autoconf/compile \
+	config/autoconf/config.guess \
+	config/autoconf/config.sub \
+	config/autoconf/depcomp \
+	config/autoconf/install-sh \
+	config/autoconf/ltmain.sh \
+	config/autoconf/mdate-sh \
+	config/autoconf/missing \
+	config/autoconf/texinfo.tex \
+	config/m4/libtool.m4 \
+	config/m4/ltoptions.m4 \
+	config/m4/ltsugar.m4 \
+	config/m4/ltversion.m4 \
+	config/m4/lt~obsolete.m4 \
+	$(DIST_ARCHIVES)
+
+maintainer-clean-local:
+	-chmod -R a+rw $(distdir)
+	-rm -fr $(distdir)
+
diff --git a/autogen.sh b/autogen.sh
new file mode 100755
index 0000000..29db9c9
--- /dev/null
+++ b/autogen.sh
@@ -0,0 +1,22 @@
+#!/bin/bash
+
+#
+# usage:
+#
+# banner &lt;target name&gt;
+#
+banner() {
+	echo
+	TG=`echo $1 | sed -e &quot;s,/.*/,,g&quot;`
+	LINE=`echo $TG |sed -e &quot;s/./-/g&quot;`
+	echo $LINE
+	echo $TG
+	echo $LINE
+	echo
+}
+
+banner &quot;autoreconf&quot;
+
+autoreconf --force --install --symlink -Wall || exit $?
+
+banner &quot;Finished&quot;
diff --git a/config/m4/.secret-world-domination-project b/config/m4/.secret-world-domination-project
new file mode 100644
index 0000000..e69de29
diff --git a/configure.ac b/configure.ac
new file mode 100644
index 0000000..919ed5f
--- /dev/null
+++ b/configure.ac
@@ -0,0 +1,156 @@
+# -*- Autoconf -*-
+# Process this file with autoconf to produce a configure script.
+AC_PREREQ(2.59)
+
+AC_INIT([can-utils], [trunk], [<A HREF="https://lists.berlios.de/mailman/listinfo/socketcan-core">socketcan-core at lists.berlios.de</A>])
+AC_CONFIG_SRCDIR([lib.c])
+AC_CONFIG_MACRO_DIR([config/m4])
+AC_CONFIG_AUX_DIR([config/autoconf])
+AC_CANONICAL_BUILD
+AC_CANONICAL_HOST
+
+#AM_MAINTAINER_MODE
+
+CFLAGS=&quot;${CFLAGS} -Wall&quot;
+
+#
+# Checks for programs.
+#
+AC_PROG_CC
+#AM_MISSING_PROG(PERL, perl, $missing_dir)
+# libtool, old:
+AC_LIBTOOL_WIN32_DLL
+#AC_LIBTOOL_TAGS([])
+AC_PROG_LIBTOOL
+# libtool, new:
+# LT_INIT(win32-dll)
+
+AM_INIT_AUTOMAKE([foreign no-exeext dist-bzip2])
+
+
+#
+# Checks for header files.
+#
+AC_CHECK_HEADERS([ \
+	fcntl.h \
+	limits.h \
+	locale.h \
+	stdint.h \
+	stdlib.h \
+	string.h \
+	syslog.h \
+	termios.h \
+	unistd.h \
+	\
+	netinet/in.h \
+	\
+	sys/ioctl.h \
+	sys/socket.h \
+	sys/time.h \
+])
+
+
+#
+# Checks for typedefs, structures, and compiler characteristics.
+#
+AC_C_INLINE
+AC_TYPE_PID_T
+AC_TYPE_UINT64_T
+
+
+#
+# Checks for library functions.
+#
+AC_FUNC_FORK
+AC_FUNC_MKTIME
+AC_CHECK_FUNCS([ \
+	alarm \
+	gettimeofday \
+	localtime_r \
+	memset \
+	select \
+	setlocale \
+	socket \
+	strchr \
+	strerror \
+	strstr \
+	strtoul \
+])
+
+
+#
+# check for SOCKETCAN specific defines and headers
+#
+AC_ARG_WITH(linux-headers,
+    AS_HELP_STRING([--with-linux-headers=/path/to/kernel/include], [path to linux kernel headers]),
+    [case &quot;${withval}&quot; in
+	(&quot;&quot;|y|ye|yes|n|no)
+	    AC_MSG_ERROR([You must supply a PATH as argument to --with-linux-headers]) ;;
+	(*)
+	    LINUX_HEADERS_DIR=&quot;$(cd &quot;${withval}&quot; &amp;&amp; pwd)&quot;
+	    if test $? -ne 0; then
+	        AC_MSG_ERROR([&quot;${withval}&quot; seems to be an invalid path!])
+	    fi ;;
+    esac]
+)
+
+maybe_linux_headers_dir=&quot;../kernel/2.6/include&quot;
+if test -z &quot;${LINUX_HEADERS_DIR}&quot; -a -e &quot;${maybe_linux_headers_dir}&quot;; then
+    LINUX_HEADERS_DIR=&quot;$(cd &quot;${maybe_linux_headers_dir}&quot; &amp;&amp; pwd)&quot;
+fi
+if test -n &quot;${LINUX_HEADERS_DIR}&quot;; then
+    linux_CFLAGS=&quot;-I${LINUX_HEADERS_DIR}&quot;
+fi
+CPPFLAGS=&quot;${CPPFLAGS} ${linux_CFLAGS}&quot;
+AC_SUBST(linux_CFLAGS)
+
+
+AC_CHECK_HEADERS([ \
+	linux/can/gw.h \
+	linux/can/isotp.h \
+	],[],[],
+[
+#ifdef HAVE_SYS_SOCKET_H
+#include &lt;sys/socket.h&gt;
+#endif
+])
+
+AM_CONDITIONAL(CONFIG_GW, [test &quot;${ac_cv_header_linux_can_gw_h}&quot; = &quot;yes&quot;])
+AM_CONDITIONAL(CONFIG_ISOTP, [test &quot;${ac_cv_header_linux_can_isotp_h}&quot; = &quot;yes&quot;])
+
+
+AC_CHECK_DECL(SO_RXQ_OVFL,,
+    [AC_DEFINE([SO_RXQ_OVFL], [40], [SO_RXQ_OVFL])]
+)
+AC_CHECK_DECL(PF_CAN,,
+    [AC_DEFINE([PF_CAN], [29], [PF_CAN])]
+)
+AC_CHECK_DECL(AF_CAN,,
+    [AC_DEFINE([AF_CAN], [PF_CAN], [AF_CAN])]
+)
+
+
+#
+# Debugging
+#
+AC_MSG_CHECKING([whether to enable debugging])
+AC_ARG_ENABLE(debug,
+    AS_HELP_STRING([--enable-debug], [enable debugging [[default=no]]]),
+	[case &quot;$enableval&quot; in
+	(y | yes) CONFIG_DEBUG=yes ;;
+        (*) CONFIG_DEBUG=no ;;
+    esac],
+    [CONFIG_DEBUG=no])
+AC_MSG_RESULT([${CONFIG_DEBUG}])
+if test &quot;${CONFIG_DEBUG}&quot; = &quot;yes&quot;; then
+    CFLAGS=&quot;${CFLAGS} -Wsign-compare -Wfloat-equal -Wformat-security -g -O1&quot;
+    AC_DEFINE(DEBUG, 1, [debugging])
+else
+    CFLAGS=&quot;${CFLAGS} -O2&quot;
+fi
+
+
+AC_CONFIG_FILES([
+	GNUmakefile
+	])
+AC_OUTPUT
-- 
1.7.0.4


</PRE>

<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="004296.html">[PATCH 2/3] add .gitignore
</A></li>
	<LI>Next message: <A HREF="004293.html">[PATCH 0/3] RFC: autotoolization
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#4291">[ date ]</a>
              <a href="thread.html#4291">[ thread ]</a>
              <a href="subject.html#4291">[ subject ]</a>
              <a href="author.html#4291">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="https://lists.berlios.de/mailman/listinfo/socketcan-core">More information about the Socketcan-core
mailing list</a><br>
</body></html>
